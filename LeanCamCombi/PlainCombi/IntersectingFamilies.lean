/-
Copyright (c) 2025 Yahel Manor. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yahel Manor
-/
module

public import Mathlib.Algebra.Order.BigOperators.Group.Finset
public import Mathlib.Algebra.Order.Sub.Basic
public import Mathlib.Combinatorics.SetFamily.Intersecting
public import Mathlib.Data.Finset.NAry
public import Mathlib.Data.Finset.Slice
public import Mathlib.SetTheory.Cardinal.Finite

/-!
# Upper Bound on `l`-Intersecting Families

## Main Predicate

* `IsIntersectingFamily l 𝒜`: every two distinct elements have intersection of size at
  least `l`.

## Main statements

* `IsIntersectingFamily.inter_card_ge_of_sized`
* `IsIntersectingFamily.card_le_of_sized`
-/

@[expose] public section

open Fintype

variable {α : Type*} [DecidableEq α]

abbrev IsIntersectingFamily (l : ℕ) (𝒜 : Set (Finset α)) : Prop :=
  (Set.Ici l).IsIntersectingOf 𝒜

/-- Bridge from the pairwise-distinct intersection hypothesis to an all-pairs cardinality bound.

If every set in `𝒜` has size `r`, `k ≤ r`, and `𝒜` is `k`-intersecting in the sense
`IsIntersectingFamily k 𝒜`, then for any `a, b ∈ 𝒜` we have `k ≤ (a ∩ b).card`.
-/
theorem IsIntersectingFamily.inter_card_ge_of_sized {k r : ℕ} {𝒜 : Set (Finset α)}
    (sized : 𝒜.Sized r) (k_le_r : k ≤ r) (inter : IsIntersectingFamily k 𝒜)
    {a b : Finset α} (ha : a ∈ 𝒜) (hb : b ∈ 𝒜) : k ≤ (a ∩ b).card := by
  rcases eq_or_ne a b with rfl | hab
  · simpa [sized ha] using k_le_r
  · exact inter ha hb hab

variable [Fintype α]

open Finset in
/-- Upper bound for large finite universes.

Let `𝒜` be a family of `r`-subsets of a finite type `α`. If `𝒜` is `l`-intersecting in the sense
`IsIntersectingFamily l 𝒜` and `card α` is large enough, then
`Nat.card 𝒜 ≤ ((card α) - l).choose (r - l)`.
-/
theorem IsIntersectingFamily.card_le_of_sized {l r : ℕ} {𝒜 : Set (Finset α)}
  (sized𝒜 : Set.Sized r 𝒜) (inter : IsIntersectingFamily l 𝒜)
  (n_much_bigger_r : 2 ^ (3 * r) * r * r + 5 * r ≤ card α) :
  Nat.card 𝒜 ≤ ((card α) - l).choose (r - l) := by
    lift 𝒜 to Finset (Finset α) using 𝒜.toFinite with ℬ hℬ
    have hcard : Nat.card (↑↑ℬ : Set (Finset α)) = #ℬ := by simp
    rw [hcard]
    have sizedℬ : Set.Sized r (ℬ : Set (Finset α)) := by simpa [hℬ] using sized𝒜
    have interℬ : IsIntersectingFamily l (ℬ : Set (Finset α)) := by
      simpa [hℬ] using inter
    clear inter
    obtain rfl | ⟨el, el_in_ℬ⟩ := ℬ.eq_empty_or_nonempty
    · simp only [Finset.card_empty, zero_le]
    simp [Set.Sized] at sizedℬ
    have r_le_card_α := card_le_card (subset_univ el)
    rw [sizedℬ el_in_ℬ, card_univ] at r_le_card_α
    by_cases l_le_r : l ≤ r
    · induction l_le_r using Nat.decreasingInduction with
      | self =>
        simp only [tsub_self, Nat.choose_zero_right, Finset.card_le_one_iff]
        intro a b a_in_ℬ b_in_ℬ
        suffices a_cap_b_eq_a : a ∩ b = a from by
          apply eq_of_subset_of_card_le (inter_eq_left.mp a_cap_b_eq_a)
          simp [sizedℬ a_in_ℬ, sizedℬ b_in_ℬ]
        apply eq_of_subset_of_card_le inter_subset_left
        calc
          #a = r := by simp [sizedℬ a_in_ℬ]
          _ ≤ #(a ∩ b) := by
            exact IsIntersectingFamily.inter_card_ge_of_sized sizedℬ le_rfl interℬ a_in_ℬ b_in_ℬ
      | of_succ k k_leq_r ind =>
        have interℬ' : ∀ ⦃a b : Finset α⦄, a ∈ ℬ → b ∈ ℬ → k ≤ #(a ∩ b) := by
          intro a b ha hb
          exact IsIntersectingFamily.inter_card_ge_of_sized sizedℬ (Nat.le_of_lt k_leq_r) interℬ ha hb
        by_cases inter_succ_k : IsIntersectingFamily (k + 1) (ℬ : Set (Finset α))
        · calc
          _ ≤ (card α - (k + 1)).choose (r - (k + 1)) := ind inter_succ_k
          _ = (card α - (k + 1)).choose (card α - (k + 1) - (r - (k + 1))) := by
            rw [Nat.choose_symm]; omega
          _ = (card α - (k + 1)).choose (card α - r) := by congr 1;omega
          _ = (card α - k - 1).choose (card α - r) := by congr 1
          _ ≤ (card α - k).choose (card α - r) := by apply Nat.choose_mono; omega
          _ ≤ (card α - k).choose (card α - k - (card α - r)) := by
            rw [Nat.choose_symm]; omega
          _ = (card α - k).choose (r - k) := by congr 1; omega
        have inter_succ_k' : ∃ A₁ ∈ ℬ, ∃ A₂ ∈ ℬ, A₁ ≠ A₂ ∧ #(A₁ ∩ A₂) ≤ k := by
          simpa [IsIntersectingFamily, Set.IsIntersectingOf, Set.Pairwise, Set.mem_Ici,
            Nat.succ_le_iff, not_forall, Classical.not_imp] using inter_succ_k
        obtain ⟨A₁, A₁_in_ℬ, A₂, A₂_in_ℬ, card_x₁_x₂⟩ := inter_succ_k'
        have k_le_inter := interℬ' A₁_in_ℬ A₂_in_ℬ
        have inter_eq_k : #(A₁ ∩ A₂) = k := Eq.symm (Nat.le_antisymm k_le_inter card_x₁_x₂.2)
        by_cases s_eq_inter_all : ∃ s, k ≤ #s ∧ ∀ a ∈ ℬ, s ⊆ a
        · obtain ⟨s, _, s_inter_a⟩ := s_eq_inter_all
          have card_map_ℬ_eq_cardℬ : #(image (· \ s) ℬ) = #ℬ := by
            refine card_image_iff.mpr ?_
            simp [Set.InjOn]
            intro x₁ x₁_in_ℬ x₂ x₂_in_ℬ x₁_sub_eq_x₂_sub
            ext a
            by_cases a_in_s : a ∈ s
            · exact (iff_true_right (s_inter_a x₂ x₂_in_ℬ a_in_s)).mpr (s_inter_a x₁ x₁_in_ℬ a_in_s)
            · have a_x_iff_a_in_mp : ∀ x ∈ ℬ, a ∈ x ↔ a ∈ ((· \ s) x) := by
                simp only [mem_sdiff, iff_self_and]
                exact fun _ _ _ => a_in_s
              rw [a_x_iff_a_in_mp x₁ x₁_in_ℬ, a_x_iff_a_in_mp x₂ x₂_in_ℬ]
              exact Eq.to_iff (congrFun (congrArg Membership.mem x₁_sub_eq_x₂_sub) a)
          have sized_map_ℬ : image (· \ s) ℬ ⊆ powersetCard (r - #s) (univ \ s) := by
            simp [powersetCard, subset_iff]
            intro x x_in_ℬ
            exists ((· \ s) x).1
            simp only [card_val, exists_prop, and_true]
            constructor
            · simp only [sdiff_val]
              refine Multiset.sub_le_sub_right ?_
              simp
            · rw [card_sdiff, inter_eq_left.mpr (s_inter_a x x_in_ℬ), sizedℬ x_in_ℬ]
          rw [← card_map_ℬ_eq_cardℬ]
          apply le_trans (card_le_card sized_map_ℬ)
          simp [card_sdiff]
          rw [← (Nat.choose_symm (Nat.sub_le_sub_right r_le_card_α #s))]
          rw [← (Nat.choose_symm (Nat.sub_le_sub_right r_le_card_α k))]
          have : #s ≤ r := by
            rw [← sizedℬ el_in_ℬ]
            exact card_le_card (s_inter_a el el_in_ℬ)
          have α_sub_s_sub_r_sub_s_ : card α - #s - (r - #s) = card α - r := by omega
          have α_sub_k_sub_r_sub_k_ : card α - k - (r - k) = card α - r := by omega
          rw [α_sub_s_sub_r_sub_s_, α_sub_k_sub_r_sub_k_]
          refine Nat.choose_le_choose (card α - r) ?_
          omega
        simp at s_eq_inter_all
        have ⟨A₃, A₃_in_ℬ, A₃_prop⟩ := s_eq_inter_all (A₁ ∩ A₂) k_le_inter
        have inter_lt_k : #((A₁ ∩ A₂) ∩ A₃) < k := by
          by_contra k_le_inter₃
          simp [← inter_eq_k, ← (card_inter_add_card_sdiff (A₁ ∩ A₂) A₃)] at k_le_inter₃
          trivial
        let U := A₁ ∪ A₂ ∪ A₃
        have card_U : #U ≤ 3 * r := by
          simp [U]
          calc
            #(A₁ ∪ (A₂ ∪ A₃)) ≤ #A₁ + #(A₂ ∪ A₃) := card_union_le A₁ (A₂ ∪ A₃)
            _ ≤ #A₁ + (#A₂ + #A₃) := by gcongr; exact card_union_le ..
            _ ≤ r + (r + r) := by gcongr <;> exact Nat.le_of_eq (sizedℬ ‹_›)
            _ = 3 * r := by omega
        have : k ≤ #U := by
          calc
            k ≤ r := by omega
            _ = #A₁ := by rw [sizedℬ A₁_in_ℬ]
            _ ≤ #U := by apply card_le_card; simp [U]
        have succ_k_le_inter_a_U : ∀ a ∈ ℬ, k + 1 ≤ #(a ∩ U) := by
          by_contra! ex
          obtain ⟨a, a_in_ℬ, a_inter_le_k⟩ := ex
          have k_le_inter_U : k ≤ #(a ∩ U) := by
            calc
              k ≤ #(a ∩ A₁) := interℬ' a_in_ℬ A₁_in_ℬ
              _ ≤ #(a ∩ U) := by
                apply card_le_card
                exact inter_subset_inter_left (by simp [U])
          have card_inter_eq_k : #(a ∩ U) = k := by omega
          simp [U] at card_inter_eq_k
          rw [← union_assoc, union_comm, inter_union_distrib_left, inter_union_distrib_left]
            at card_inter_eq_k
          have _ := calc
            k ≤ k + k - k := by simp
            _ ≤ k + k - #(a ∩ (A₁ ∪ A₂)) := by
              apply Nat.sub_le_sub_left
              simp [← card_inter_eq_k, card_le_card, inter_union_distrib_left]
            _ ≤ k + k - #(a ∩ A₁ ∪ (a ∩ A₂)) := by simp [inter_union_distrib_left]
            _ ≤ #(a ∩ A₁) + #(a ∩ A₂) - #(a ∩ A₁ ∪ (a ∩ A₂)) := by
              gcongr <;> exact interℬ' a_in_ℬ ‹_›
            _ = #((a ∩ A₁) ∩ (a ∩ A₂)) := Eq.symm (card_inter (a ∩ A₁) (a ∩ A₂))
            _ = #(a ∩ (A₁ ∩ A₂)) := by congr 1; exact Eq.symm (inter_inter_distrib_left a A₁ A₂)
          have k_lt_k := calc
            k = k + k - k := by simp
            _ < k + k - #((A₁ ∩ A₂) ∩ A₃) := by
              refine (tsub_lt_tsub_iff_left_of_le ?_).mpr inter_lt_k
              omega
            _ ≤ k + k - #(a ∩ (A₃ ∩ (A₁ ∩ A₂))) := by
              gcongr k + k - #?_
              nth_rw 2 [inter_comm]
              exact inter_subset_right
            _ ≤ #(a ∩ A₃) + #(a ∩ (A₁ ∩ A₂)) - #(a ∩ (A₃ ∩ (A₁ ∩ A₂))) := by
              solve_by_elim [Nat.sub_le_sub_right, Nat.add_le_add (interℬ' a_in_ℬ A₃_in_ℬ)]
            _ = #(a ∩ A₃) + #(a ∩ (A₁ ∩ A₂)) - #(a ∩ A₃ ∩ (a ∩ (A₁ ∩ A₂))) := by
              congr 2
              rw [inter_inter_distrib_left]
            _ = #(a ∩ A₃ ∪ (a ∩ (A₁ ∩ A₂))) := by rw [card_union]
            _ = #(a ∩ (A₃ ∪ (A₁ ∩ A₂))) := by rw [inter_union_distrib_left]
            _ ≤ #(a ∩ U) := by
              gcongr
              simp [U]
              rw [union_comm, ← union_assoc]
              apply_rules [inter_subset_inter_left, union_subset_union_left, inter_subset_union]
            _ ≤ k := Nat.le_of_lt_succ a_inter_le_k
          exact (Nat.lt_irrefl _ k_lt_k).elim
        have card_ℬ_leq_prod : #ℬ ≤ #U.powerset * #{p : Finset α | ∃ a ∈ ℬ, a \ U = p} := by
          let fn : Finset α → Finset α → Finset α := fun a b ↦ a ∪ b
          rw [← ((@card_image₂_iff _ _ _ _ fn U.powerset {p : Finset α | ∃ a ∈ ℬ, a \ U = p}).mpr ?_)]
          apply card_le_card
          rw [subset_iff]
          intro x x_in_ℬ
          simp [fn]
          exists x ∩ U
          simp
          exists x
          rw [union_comm, sdiff_union_inter]
          simp [x_in_ℬ]
          simp [Set.InjOn, fn]
          intro a b a_in_U x x_in_ℬ x_minus_U_eq_b a' b' a'_in_U x' x'_in_ℬ x'_minus_U_eq_b cup_eq
          constructor
          · have a_cup_b_cap_u_eq_a : (a ∪ b) ∩ U = a := by
              rw [← x_minus_U_eq_b, inter_comm, inter_union_distrib_left]
              simpa
            have a'_cup_b'_cap_u_eq_a' : (a' ∪ b') ∩ U = a' := by
              rw [← x'_minus_U_eq_b, inter_comm, inter_union_distrib_left]
              simpa
            rw [← a_cup_b_cap_u_eq_a, ← a'_cup_b'_cap_u_eq_a', cup_eq]
          · have a_cup_b_sdiff_u_eq_a : (a ∪ b) \ U = b := by
              rw [union_sdiff_distrib, ← x_minus_U_eq_b, (sdiff_eq_empty_iff_subset).mpr a_in_U]
              simp
            have a'_cup_b'_sdiff_u_eq_a' : (a' ∪ b') \ U = b' := by
              rw [union_sdiff_distrib, ← x'_minus_U_eq_b, (sdiff_eq_empty_iff_subset).mpr a'_in_U]
              simp
            rw [← a_cup_b_sdiff_u_eq_a, ← a'_cup_b'_sdiff_u_eq_a', cup_eq]
        have card_filt_le_chooce : #(filter (fun p ↦ ∃ a ∈ ℬ, a \ U = p) univ)
          ≤ (card α - #U).choose (r - (k + 1)) * r := by
          calc
            #{p | ∃ a ∈ ℬ, a \ U = p}
              ≤ #((range (r - k)).biUnion fun n' ↦ powersetCard n' (univ \ U)) := card_le_card ?_
            _ ≤ (card α - #U).choose (r - (k + 1)) * (r - k) := ?_
            _ ≤ (card α - #U).choose (r - (k + 1)) * r := by apply Nat.mul_le_mul_left; omega
          · simp [subset_iff]
            intro a a_in_ℬ
            rw [← sizedℬ a_in_ℬ, ← card_sdiff_add_card_inter a U, Nat.lt_sub_iff_add_lt]
            apply Nat.add_lt_add_left
            exact succ_k_le_inter_a_U a a_in_ℬ
          · rw [mul_comm]
            nth_rw 2 [← card_range (r - k)]
            apply card_biUnion_le_card_mul
            intro lvl lvl_in_range
            simp only [mem_range] at lvl_in_range
            simp [card_univ_diff U]
            have lvl_lt_r_sub_succ_k : lvl ≤ r - (k + 1) := by omega
            induction lvl_lt_r_sub_succ_k using Nat.decreasingInduction with
            | self => simp
            | of_succ lvl' _ ind =>
              have _ := @Nat.choose_le_succ_of_lt_half_left lvl' (card α - #U) ?_
              all_goals omega
        calc
          #ℬ ≤ #U.powerset * #(filter (fun p ↦ ∃ a ∈ ℬ, a \ U = p) univ) := card_ℬ_leq_prod
          _ ≤ 2 ^ #U * #(filter (fun p ↦ ∃ a ∈ ℬ, a \ U = p) univ) := by
            simp only [card_powerset, le_refl, U]
          _ ≤ 2 ^ #U * ((card α - #U).choose (r - (k + 1)) * r) := by gcongr
          _ ≤ 2 ^ #U * ((card α - k).choose (r - (k + 1)) * r) := by
            apply_rules [Nat.mul_le_mul_left, Nat.mul_le_mul_right, Nat.choose_mono, Nat.sub_le_sub_left]
          _ ≤ 2 ^ (3 * r) * ((card α - k).choose (r - (k + 1)) * r) := by gcongr; simp
          _ ≤ (2 ^ (3 * r) * (r * (card α - k).choose (r - (k + 1) + 1) * (r - (k + 1) + 1)) /
            (card α - k - (r - (k + 1)))) := by
            rw [Nat.le_div_iff_mul_le, mul_assoc, mul_comm ((card α - k).choose (r - (k + 1))) r,
              mul_assoc, ← Nat.choose_succ_right_eq, mul_assoc]
            omega
          _ = (2 ^ (3 * r) * (r * (card α - k).choose (r - k) * (r - k)) /
            (card α - k - (r - (k + 1)))) := by congr <;> omega
          _ ≤ ((card α - k).choose (r - k) * (r - k) * (2 ^ (3 * r) * r) /
            (card α - k - (r - (k + 1)))) := by
            simp [← mul_assoc, mul_comm]
          _ ≤ (card α - k).choose (r - k) := ?_
        apply Nat.div_le_of_le_mul
        nth_rw 5 [mul_comm]
        nth_rw 1 [mul_assoc]
        refine Nat.mul_le_mul_left ((card α - k).choose (r - k)) ?_
        rw [Nat.le_sub_iff_add_le, Nat.le_sub_iff_add_le, add_assoc]
        · calc
          (r - k) * (2 ^ (3 * r) * r) + (r - (k + 1) + k) ≤ r * (2 ^ (3 * r) * r) + r := by
            gcongr <;> omega
          _ = 2 ^ (3 * r) * r * r + r := by simp [mul_comm]
          _ ≤ card α := by omega
        all_goals omega
    · have card_ℬ_le_one : #ℬ ≤ 1 := by
        refine card_le_one_iff.mpr ?_
        intro a b a_in_ℬ b_in_ℬ
        by_contra hab
        have l_le_inter : l ≤ #(a ∩ b) := interℬ a_in_ℬ b_in_ℬ hab
        have inter_le_r : #(a ∩ b) ≤ r := by
          calc
            #(a ∩ b) ≤ #a := card_le_card inter_subset_left
            _ = r := sizedℬ a_in_ℬ
        exact l_le_r (l_le_inter.trans inter_le_r)
      calc
        #ℬ ≤ 1 := card_ℬ_le_one
        _ ≤ ((card α) - l).choose (r - l) := by
          have r_lt_l : r < l := Nat.lt_of_not_ge l_le_r
          simp [Nat.sub_eq_zero_of_le (Nat.le_of_lt r_lt_l)]
